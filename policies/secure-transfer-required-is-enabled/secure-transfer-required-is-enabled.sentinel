import "tfplan/v2" as tfplan

allStorageAccounts = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.mode is "managed" and
		resource_changes.type is "azurerm_storage_account" and
		(resource_changes.change.actions contains "create" or
			resource_changes.change.actions is ["update"])
}

enable_https_traffic_only_is_true = rule {
	all allStorageAccounts as _, accounts {
		accounts.change.after.enable_https_traffic_only is not defined or
			accounts.change.after.enable_https_traffic_only is true
	}
}

https_traffic_only_is_true = rule {
	all allStorageAccounts as _, accounts {
		accounts.change.after.https_traffic_only_enabled is not defined or
			accounts.change.after.https_traffic_only_enabled is true
	}
}

// Ensure that 'Secure transfer required' is set to Enabled.
main = rule {
	enable_https_traffic_only_is_true and https_traffic_only_is_true
}
